let g:airline_powerline_fonts = 1


if !has('gui_running')
    set t_Co=256
endif


let g:lightline = {
    \   'colorscheme': 'one',
    \   'active': {
    \       'left': [
    \                [ 'mode', 'paste' ],
    \                [ 'readonly', 'filename_modified', 'gitbranch' ],
    \                [ 'aaatata']
    \               ],
    \       'right': [
    \                 [ 'percent', 'lineinfo' ],
    \                 [ 'filetype']
    \                ]
    \   },
    \   'inactive': {
    \       'left': [
    \                [ 'mode_explorer' ],
    \                [ 'readonly', 'filename_modified' ],
    \               ],
    \       'right': [
    \                 [ 'filetype']
    \                ]
    \   },
    \   'component_function': {
    \       'mode': 'LightlineMode',
    \       'mode_explorer': 'LightlineModeEx',
    \       'readonly': 'LightlineReadonly',
    \       'filename_modified': 'LightlineFilename',
    \       'gitbranch': 'LightlineBranch',
    \       'aaatata': 'LightlineAaatata',
    \       'lineinfo': 'LightlineLineinfo',
    \       'percent': 'LightlinePercent',
    \       'filetype': 'LightlineFiletype',
    \   },
    \   'separator': { 'left': 'î‚°', 'right': 'î‚²'  },
    \   'subseparator': { 'left': 'î‚±', 'right': 'î‚³'  },
    \ }

"=== === === === === === === === === === === === === === === === === === === ===
" functions
"=== === === === === === === === === === === === === === === === === === === ===
function! LightlineMode() abort
    let l:ftmap = {
                \ 'coc-explorer': 'EXPLORER',
                \ 'netrw': 'EXPLORER',
                \}
    return get(l:ftmap, &filetype, lightline#mode())
endfunction

function! LightlineModeEx() abort
    let l:ftmap = {
                \ 'coc-explorer': 'EXPLORER',
                \ 'netrw': 'EXPLORER',
                \}
    return get(l:ftmap, &filetype, '')
endfunction

function! LightlineReadonly() abort
    return winwidth(0) >= 44 ? (&readonly ? 'ï ½' : '') : ''
endfunction

function! LightlineFilename()
    let l:filename = expand('%:t') !=# '' ? expand('%:t') : '[No Name]'
    let l:modified = &modified ? ' +' : ''

    if winwidth(0) < 44
        return l:filename
    endif
    return l:filename . l:modified
endfunction

function! LightlineBranch() abort
    if &filetype ==# 'coc-explorer' || &filetype ==# 'netrw'
        return ''
    endif

    let l:branch = gitbranch#name()
    return strlen(l:branch) ? (strlen(l:branch) >= 64 ? 'î‚  ' . l:branch : 'î‚ ') : ''
endfunction

function! LightlineAaatata() abort
    return winwidth(0) >= 65 ? 'â˜œ(ï¾Ÿãƒ®ï¾Ÿâ˜œ)ðŸŽ¶' : ''
endfunction

function! LightlineLineinfo() abort
    if winwidth(0) < 45
        return ''
    endif

    let l:current_line = printf('%-3s', line('.'))
    let l:current_col  = printf('%-3s', col('.'))
    "let l:max_line = printf('%-3s', line('$'))
    let l:lineinfo = l:current_line . ':' . l:current_col "'/' . l:max_line
    return l:lineinfo
endfunction

function! LightlinePercent() abort
    if winwidth(0) < 44
        return ''
    endif

    let l:percent = line('.') * 100 / line('$') . '%'
    return printf('%-4s', l:percent)
endfunction

function! LightlineFiletype() abort
    let l:icon = WebDevIconsGetFileTypeSymbol()

    if &filetype ==# 'coc-explorer'
        let l:branch = gitbranch#name()
        return strlen(l:branch) ? (strlen(l:branch) <= 9 ? 'î‚  ' . l:branch : 'î‚ ') : l:icon
    endif

    return winwidth(0) >= 65 ? (strlen(&filetype) ? l:icon .' '. &filetype : l:icon) : l:icon
endfunction
